apply from: 'gradle/buildtools/ColoredOutput.gradle'
project.ext.set("TRAVIS_FOLDING", true)

apply plugin: 'java-library'

apply plugin: 'maven'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}

group = "com.maxondev.jsif"
archivesBaseName = "jsif"
version = "0.1.1-SNAPSHOT"

if (hasProperty('ossrhUsername'))
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'JVM Self Initializing Fake'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Self Initializing Fake library for the JVM (inspired by https://martinfowler.com/bliki/SelfInitializingFake.html)'
                    url 'https://github.com/maximn/jsif'

                    scm {
                        connection 'scm:git:https://github.com/maximn/jsif.git'
                        developerConnection 'scm:git:https://github.com/maximn/jsif.git'
                        url 'https://github.com/maximn/jsif.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'maxim'
                            name 'Maxim Novak'
                            email 'maxim.novak@gmail.com'
                        }
                    }
                }
            }
        }
    }

repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'

    compile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.12.0'
    testCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.4'
    testCompile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.4'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
}

